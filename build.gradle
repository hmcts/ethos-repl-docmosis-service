buildscript {
    ext {
        springBootVersion = '2.2.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath group: 'org.sonarsource.scanner.gradle', name: 'sonarqube-gradle-plugin', version: '2.6.2'
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.2.0")
    }
}

plugins {
    id 'jacoco'
    id 'net.ltgt.apt' version '0.10'
    id 'org.sonarqube' version '2.8'
    id "org.owasp.dependencycheck" version "5.3.2.1"
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.sonarqube'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'jacoco'
apply plugin: 'net.serenity-bdd.aggregator'

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

group = 'uk.gov.hmcts.ethos.replacement'
//version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url 'https://repo.spring.io/libs-milestone' }
}

def versions = [
        springfoxSwagger: '2.9.2',
        logging: '5.1.0'
]

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-configuration-processor")
    compile('org.springframework.boot:spring-boot-starter-data-jpa')

    compile('org.postgresql:postgresql')

    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'

    compile(group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger)
    compile (group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger)
    compile group: 'org.apache.poi', name: 'poi', version: '4.0.0'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '4.0.0'

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
    compile group: 'uk.gov.hmcts.reform', name: 'document-management-client', version: '6.0.0'
    compile group: 'uk.gov.hmcts.reform', name: 'service-auth-provider-client', version: '2.0.0'

    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.0.2.RELEASE'
    compile group: 'io.github.openfeign', name: 'feign-httpclient', version: '10.0.1'
    compile group: 'io.github.openfeign', name: 'feign-core', version: '9.5.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.logging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.logging

    compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: '0.0.5'
    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version:'0.0.4'
    compile group: 'org.springframework.retry', name: 'spring-retry'

    compile group: 'org.elasticsearch', name: 'elasticsearch', version: '7.6.1'

    compile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '8.10'

    testCompile group: 'junit', name: 'junit', version: '4.13'
    testCompile group: 'io.rest-assured', name: 'rest-assured', version: '4.3.0'
    testCompile (group: 'org.docx4j', name: 'docx4j', version: '6.1.2') {
        exclude module: 'slf4j-log4j12'
    }
    testCompile 'org.projectlombok:lombok:1.18.12'
    testCompile group: 'com.auth0', name: 'java-jwt', version: '3.10.1'
    testCompile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    testCompile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.2.5.RELEASE'
    testCompile group: 'com.warrenstrange', name: 'googleauth', version: '1.4.0'

    testCompile group: 'net.serenity-bdd', name: 'serenity-core', version: '2.2.0'
    testCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: '2.2.0'
    testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: '2.2.0'
    testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: '2.2.0'
}

jar {
    archiveName 'ethos-repl-docmosis-service.jar'
}

def sonarExclusions = ['**/uk/gov/hmcts/ethos/replacement/docmosis/config/**',
                       '**/uk/gov/hmcts/ethos/replacement/docmosis/model/**',
                       '**/uk/gov/hmcts/reform/ethos/replacement/docmosis/CaseOrchestrationApplication'
]

sonarqube {
    println "echo sonarqube"
    properties {
        property "sonar.exclusions", sonarExclusions.join(', ')
        property "sonar.projectKey", "ethos-repl-docmosis-service"
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sourceSets {
    test {
        java.srcDir 'src/test/java'
    }
    functional {
        java.srcDir 'src/test/functional/java'
        resources.srcDir 'src/test/functional/resources'
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    functionalCompile.extendsFrom testCompile
    functionalRuntime.extendsFrom testRuntime
}

test {
    exclude '**/*ComponentTest*'
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.functional.output.classesDirs
    classpath = sourceSets.functional.runtimeClasspath

    useJUnit {
        includeCategories 'uk.gov.hmcts.ethos.replacement.functional.SmokeTest'
    }
    systemProperty('VAULTNAME', System.getenv('VAULTNAME'))
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
    testClassesDirs = sourceSets.functional.output.classesDirs
    classpath = sourceSets.functional.runtimeClasspath

    useJUnit {
        includeCategories 'uk.gov.hmcts.ethos.replacement.functional.SmokeTest'
    }

    systemProperty('VAULTNAME', System.getenv('VAULTNAME'))
    //systemProperty('tags', 'FunctionalTest')

    maxHeapSize = '1G'
    finalizedBy aggregate
}

jacocoTestReport {
    executionData(test)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }
}
